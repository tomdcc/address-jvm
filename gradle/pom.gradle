/*
 * Copyright 2012 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
	ext.poms = []
	afterEvaluate {
		poms*.whenConfigured { pom ->
			pomModifications.each {
				configure(pom, it)
			}
		}
	}
	pomModifications = []
	modifyPom = { pomModifications << it }
}

tasks.withType(Upload) {
	repositories.withType(org.gradle.api.artifacts.maven.MavenResolver) {
		poms << it.pom
	}
}

modifyPom {
	project {
		url "https://github.com/tomdcc/address-jvm/"
		inceptionYear "2013"
		licenses {
			license {
				name 'The Apache Software License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				distribution 'repo'
			}
		}
		scm {
			url "https://github.com/tomdcc/address-jvm/"
		}
		developers {
			developer {
				id "tomdcc"
				name "Tom Dunstan"
			}
		}
	}
	
	dependencies.removeAll(dependencies.findAll { it.scope in ["test", "provided"] })
}